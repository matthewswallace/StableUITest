apply plugin: 'com.android.application'

android {
	compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
	buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION
	
	defaultConfig {
		applicationId "com.sample.stableuitest"
		minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
		targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
		versionCode Integer.parseInt(project.VERSION_CODE)
		versionName project.VERSION_NAME
	}
	
	
	File signingFile = file('signing.properties')
	if(signingFile.exists()) {
		Properties signing = new Properties()
		signing.load(new FileInputStream(signingFile))
		
		signingConfigs {
			release {
				storeFile file(signing["KEY_STORE"])
				storePassword signing["KEY_STORE_PASSWORD"]
				keyAlias signing["KEY_STORE_ALIAS"]
				keyPassword signing["KEY_STORE_ALIAS_PASSWORD"]
			}
		}
	} else {
		signingConfigs {
			release
		}
	}
	
	
	buildTypes {
		release {
			minifyEnabled false
			signingConfig signingConfigs.release
		}
	}
	
	android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
			
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                output.outputFile = new File(outputFile.parent, "StableUITest-" + variant.buildType.name + ".apk")
            }
        }
    }
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])
	compile project(':deps:extension-api')
	
}